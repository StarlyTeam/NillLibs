plugins {
    id "maven-publish"
    id "io.github.goooler.shadow" version "8.1.7"
}

group = "kr.starly"
version = "1.2"

allprojects {
    apply plugin: "java"
    apply plugin: "java-library"

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://libraries.minecraft.net/" } // authlib
        maven { url "https://repo.papermc.io/repository/maven-public/" } // paper-api
        maven { url "https://repo.codemc.io/repository/maven-public/" } // item-nbt-api
        maven { url "https://repo.xenondevs.xyz/releases" } // invui
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:24.1.0"
        compileOnly "org.projectlombok:lombok:1.18.32"
        annotationProcessor "org.projectlombok:lombok:1.18.32"
        compileOnly "io.netty:netty-all:5.0.0.Alpha2"
        compileOnly "org.json:json:20240303"

        compileOnly "com.mojang:authlib:6.0.54"
        compileOnly "io.papermc.paper:paper-api:1.18-R0.1-SNAPSHOT"
        compileOnly "de.tr7zw:item-nbt-api:2.13.0"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot:1.20.6-R0.1-SNAPSHOT:remapped-mojang"
    api "org.json:json:20240303"
    api "de.tr7zw:item-nbt-api:2.13.0"
    api "xyz.xenondevs.invui:invui:1.32"

    api project(":nms:common")
    api fileTree("/target/nms") { include "*.jar" }
}

shadowJar {
    archiveFileName = "NillLibs.jar"
    destinationDirectory = file("/target")

    relocate "org.json", "kr.starly.libs.json"
    relocate "de.tr7zw.changeme.nbtapi", "kr.starly.libs.nbtapi"
    relocate("xyz.xenondevs.invui.util", "kr.starly.libs.util") {
        exclude "xyz.xenondevs.invui.util.MojangApiUtils"
    }
    relocate("xyz.xenondevs.invui", "kr.starly.libs.inventory") {
        exclude "xyz.xenondevs.invui.util"
    }
    relocate "xyz.xenondevs.inventoryaccess", "kr.starly.libs.inventory.access"
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    repositories {
        mavenLocal()
    }
}

assemble.dependsOn shadowJar