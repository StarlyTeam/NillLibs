plugins {
    id "maven-publish"
    id "io.github.goooler.shadow" version "8.1.7"
}

group = "kr.starly"
version = "1.2"

allprojects {
    apply plugin: "java"
    apply plugin: "java-library"

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://libraries.minecraft.net/" } // authlib
        maven { url "https://repo.papermc.io/repository/maven-public/" } // paper-api
        maven { url "https://repo.codemc.io/repository/maven-public/" } // item-nbt-api
        maven { url "https://repo.xenondevs.xyz/releases" } // invui
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:24.1.0"
        compileOnly "org.projectlombok:lombok:1.18.32"
        annotationProcessor "org.projectlombok:lombok:1.18.32"
        compileOnly "io.netty:netty-all:5.0.0.Alpha2"
        compileOnly "org.json:json:20240303"

        compileOnly "com.mojang:authlib:6.0.54"
//        compileOnly "io.papermc.paper:paper-api:1.18-R0.1-SNAPSHOT"
        compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
        compileOnly "de.tr7zw:item-nbt-api:2.13.0"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot:1.20.6-R0.1-SNAPSHOT:remapped-mojang"
    api "org.json:json:20240303"
    api "de.tr7zw:item-nbt-api:2.13.0"
    api "xyz.xenondevs.invui:invui:1.32"

    api project(":nms:common")
    api project(":nms:v1_18_R1")
    api project(":nms:v1_18_R2")
    api project(":nms:v1_19_R1")
    api project(":nms:v1_19_R2")
    api project(":nms:v1_19_R3")
    api project(":nms:v1_20_R1")
    api project(":nms:v1_20_R2")
    api project(":nms:v1_20_R3")
    api project(":nms:v1_20_R4")
}

shadowJar {
    archiveFileName = "NillLibs.jar"
    destinationDirectory = file("/target")

    relocate "org.json", "kr.starly.libs.json"
    relocate "de.tr7zw.changeme.nbtapi", "kr.starly.libs.nbtapi"
    relocate "xyz.xenondevs.inventoryaccess", "kr.starly.libs.inventory.access"
    relocate "xyz.xenondevs.invui", "kr.starly.libs.inventory"
    relocate "xyz.xenondevs.invui.util.ArrayUtils", "kr.starly.libs.util.ArrayUtils"
    relocate "xyz.xenondevs.invui.util.ColorPalette", "kr.starly.libs.util.ColorPalette"
    relocate "xyz.xenondevs.invui.util.DataUtils", "kr.starly.libs.util.DataUtils"
    relocate "xyz.xenondevs.invui.util.InventoryUtils", "kr.starly.libs.util.InventoryUtils"
    relocate "xyz.xenondevs.invui.util.ItemUtils", "kr.starly.libs.util.ItemUtils"
    relocate "xyz.xenondevs.invui.util.MathUtils", "kr.starly.libs.util.MathUtils"
    relocate "xyz.xenondevs.invui.util.Pair", "kr.starly.libs.util.Pair"
    relocate "xyz.xenondevs.invui.util.Point2D", "kr.starly.libs.util.Point2D"
    relocate "xyz.xenondevs.invui.util.SlotUtils", "kr.starly.libs.util.SlotUtils"
    relocate "xyz.xenondevs.invui.util.TriConsumer", "kr.starly.libs.util.TriConsumer"
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    repositories {
        mavenLocal()
    }
}

build.dependsOn shadowJar